plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.beryx.jlink' version '3.0.1'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id("org.javamodularity.moduleplugin") version "1.8.15"
}

group = 'br.unicamp.cst.cli'
version = '0.2'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'info.picocli:picocli:4.7.6'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
    implementation 'org.yaml:snakeyaml:2.3'
    implementation 'com.github.javaparser:javaparser-core:3.26.3'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "STANDARD_ERROR", "STANDARD_OUT"
    }
}

application {
    mainModule = 'br.unicamp.cst.cli'
    mainClass = 'br.unicamp.cst.cli.Main'
}

/*jar {
    manifest {
        attributes "Main-Class": 'br.unicamp.cst.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
 */

javafx {
    version = 21
    modules = [
            'javafx.controls',
            'javafx.base',
            'javafx.graphics'
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    mergedModule {
        requires 'java.base'
        requires 'java.desktop'
        requires 'java.logging'
    }
    jpackage {
        //installerType = 'exe'
        outputDir = 'cst_cli'
        imageName = 'cst'
    }
    //launcher {jvmArgs = ['--add-opens cscli/br.unicamp.cst.commands=ALL-UNNAMED']}
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('test-coverage')
        dependsOn test // tests are required to run before generating the report
    }
}
